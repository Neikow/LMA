//MESH FOR WAVE PROPAGATION IN BALLAST
//L˙cio Correa (lucio.a.c@gmail.com)
//geometry based on a trackway report
// …cole Centrale Paris @ 17/09/2014

//NUMEBER OF TRAVERSEs
traverse = 20;

//GEOMETRICAL DEFINITIONS

B_h =  0.48; //ballast height (y)
B_wu = 3.0; //ballast width upper (x)
B_wl = 4.9; //ballast width lower (x)

Z = 1;

S_h =  3; //soil height (y)
S_w =  20; //soil width (x)

T_w = 2.0; //traverse width (x)
T_h = 0.2; //traverse height (y)
T_t = 0.3; //traverse thickness (z)

T_esp = 0.6; //space between 2 traverses
D_pml1 = 0.6; //
//ele_Dpml1 = 4; // homogêneo

PML_t= 18;//PML width

//MESH DEFINITIONS

//n_layers = 20; //layers number of domain
n_PML    = 2; //layers number of PML domain
n_layersTraverse = 2; //number of elements in (y) direction
ele_Dpml1 = 8; // heterogeneo
//out of ballast
n_spa = 8; // heterogeneo
n_tra = 4; // heterogeneo

//n_spa = 4; // homogêneo
//n_tra = 2; // homogêneo

//mesh size control
Lc = 0.4;
Lc_b = 0.06; // heterogeneo
//Lc_b = 0.15; // heterogeneo

// PONITS

//ballast
Point(1) = {-B_wl/2,  0,Z,Lc_b};
Point(2) = { B_wl/2,  0,Z,Lc_b};
Point(3) = { B_wu/2,B_h,Z,Lc_b};
Point(4) = {-B_wu/2,B_h,Z,Lc_b};

//soil
Point(5) = {-S_w/2,-S_h,Z,Lc};
Point(6) = { S_w/2,-S_h,Z,Lc};
Point(7) = { S_w/2, 0.0,Z,Lc};
Point(8) = {-S_w/2, 0.0,Z,Lc};

//Perfect Matched Layer (PML)
Point(9) =  {-S_w/2-PML_t,-S_h-PML_t,Z,Lc};
Point(10) = {      -S_w/2,-S_h-PML_t,Z,Lc};
Point(11) = {       S_w/2,-S_h-PML_t,Z,Lc};
Point(12) = { S_w/2+PML_t,-S_h-PML_t,Z,Lc};
Point(13) = { S_w/2+PML_t,      -S_h,Z,Lc};
Point(14) = { S_w/2+PML_t,       0.0,Z,Lc};
Point(15) = {-S_w/2-PML_t,       0.0,Z,Lc};
Point(16) = {-S_w/2-PML_t,      -S_h,Z,Lc};

//traverse
Point(17) ={-T_w/2,-T_h/2+B_h,Z,Lc_b};
Point(18) ={ T_w/2,-T_h/2+B_h,Z,Lc_b};
Point(19) ={ T_w/2,     0+B_h,Z,Lc_b};
Point(20) ={-T_w/2,     0+B_h,Z,Lc_b};

//LINES

//ballast
Line(1)={1,2};
Line(2)={2,3};
Line(3)={3,19};
Line(31)={19,20};
Line(32)={20,4};
Line(4)={4,1};

//soil
Line(5)={5,6};
Line(6)={6,7};
Line(7)={7,2};
Line(8)={2,1};
Line(9)={1,8};
Line(10)={8,5};

//PML (1)
Line(11)={16,5};
Line(12)={5,8};
Line(13)={8,15};
Line(14)={15,16};

//PML (2)
Line(15)={9,10};
Line(16)={10,5};
Line(17)={5,16};
Line(18)={16,9};

//PML (3)
Line(19)={10,11};
Line(20)={11,6};
Line(21)={6,5};
Line(22)={5,10};

//PML (4)
Line(23)={11,12};
Line(24)={12,13};
Line(25)={13,6};
Line(26)={6,11};

//PML (5)
Line(27)={6,13};
Line(28)={13,14};
Line(29)={14,7};
Line(30)={7,6};

//sleepers
Line(33)={20,17};
Line(34)={17,18};
Line(35)={18,19};

//MAKE THE LOOPS AND SURFACES

//ballast
Line Loop(1) = {1,2,3,-35,-34,-33,32,4};
Plane Surface(1) = {1};

//soil
Line Loop(2) = {5,6,7,8,9,10};
Plane Surface(2) = {2};

//PML1
Line Loop(3) = {11,12,13,14};
Plane Surface(3) = {3};

//PML2
Line Loop(4) = {15,16,17,18};
Plane Surface(4) = {4};

//PML3
Line Loop(5) = {19,20,21,22};
Plane Surface(5) = {5};

//PML4
Line Loop(6) = {23,24,25,26};
Plane Surface(6) = {6};

//PML5
Line Loop(7) = {27,28,29,30};
Plane Surface(7) = {7};

//sleepers
Line Loop(8) = {33,34,35,31};
Plane Surface(8) = {8};


// Tell Gmsh how many cells you want per edge
//Transfinite Line{1}   = n_b;
Transfinite Line{11,13,15,16,17,18,20,22,23,24,25,26,27,29}=n_PML+1;

// Tell Gmsh what the corner points are
//(going clockwise or counter-clockwise):
//Transfinite Surface{1} = {1,2,3,4}; 
//Transfinite Surface{2} = {5,6,7,8}; 
Transfinite Surface{3} = {16,5,8,15};
Transfinite Surface{4} = {9,10,5,16}; 
Transfinite Surface{5} = {10,11,6,5}; 
Transfinite Surface{6} = {11,12,13,6}; 
Transfinite Surface{7} = {6,13,14,7};
Transfinite Surface{8} = {17,18,19,20};

// Recombine the triangles into quads:
Recombine Surface{1};
Recombine Surface{2};
Recombine Surface{3};
Recombine Surface{4};
Recombine Surface{5};
Recombine Surface{6};
Recombine Surface{7};
Recombine Surface{8};

// Havent tested this yet, but doesnt seem to hurt:
Mesh.Smoothing = 200;

// Extrued for a psudo 2D mesh -PML 1- :)
Extrude {0,0,PML_t} {Surface{1,2,3,4,5,6,7,8}; Layers{n_PML}; Recombine; }

primeira = 77;
diff = 22;
// Extrued for a psudo 2D mesh -PML 1- :)
Extrude {0,0,D_pml1} {Surface{primeira,primeira+32,primeira+32+1*diff,primeira+32+2*diff,primeira+32+3*diff,primeira+32+4*diff,primeira+32+5*diff,primeira+32+6*diff}; Layers{ele_Dpml1}; Recombine; }


For(1:traverse)

primeira = primeira + 206;
// Extrued for a psudo 2D mesh -PML 1- :)
Extrude {0,0,T_t} {Surface{primeira,primeira+32,primeira+32+1*diff,primeira+32+2*diff,primeira+32+3*diff,primeira+32+4*diff,primeira+32+5*diff,primeira+32+6*diff}; Layers{n_tra}; Recombine; }

primeira = primeira + 206;
// Extrued for a psudo 2D mesh -PML 1- :)
Extrude {0,0,T_esp} {Surface{primeira,primeira+32,primeira+32+1*diff,primeira+32+2*diff,primeira+32+3*diff,primeira+32+4*diff,primeira+32+5*diff,primeira+32+6*diff}; Layers{n_spa}; Recombine; }

EndFor


primeira = primeira + 206;
// Extrued for a psudo 2D mesh -PML 1- :)
Extrude {0,0,PML_t} {Surface{primeira,primeira+32,primeira+32+1*diff,primeira+32+2*diff,primeira+32+3*diff,primeira+32+4*diff,primeira+32+5*diff,primeira+32+6*diff}; Layers{n_PML}; Recombine; }


//traverse
primeira = 652;

For(1:traverse)

Recombine Surface{primeira};

Extrude {0,T_h/2,0} {Surface{primeira}; Layers{n_layersTraverse}; Recombine; }

primeira = primeira+412;

EndFor

Physical Volume ( 1  ) = { 1,8 };
Physical Volume ( 2  ) = { 2 };
Physical Volume ( 3  ) = { 3 };
Physical Volume ( 4  ) = { 4 };
Physical Volume ( 5  ) = { 5 };
Physical Volume ( 6  ) = { 6 };
Physical Volume ( 7  ) = { 7 };

count = 1;
For(1:2*traverse+1)

V8 (count-1) = count*8 +1;
V9 (count-1) = count*8 +2;
V10(count-1) = count*8 +3;
V11(count-1) = count*8 +4;
V12(count-1) = count*8 +5;
V13(count-1) = count*8 +6;
V14(count-1) = count*8 +7;

count = count+1;
EndFor

count = 1;
For(1:traverse+1)
V15 (count-1) = count*16;
count = count+1;
EndFor

count = 1;
For(1:traverse)
V22(count-1)= count*16+8;
count = count+1;
EndFor

count = 1;
For(1:traverse)
V23 (count-1) = 8*(2*traverse+2)+8+count;
count = count+1;
EndFor

Physical Volume ( 8 ) = {V8[],V15[]};
Physical Volume ( 9 ) = {V9[]};
Physical Volume ( 10 ) = {V10[]};
Physical Volume ( 11 ) = {V11[]};
Physical Volume ( 12 ) = {V12[]};
Physical Volume ( 13 ) = {V13[]};
Physical Volume ( 14 ) = {V14[]};

Physical Volume ( 22 ) = {V22[],V23[]};

Physical Volume ( 15 ) = { 8*(2*traverse+2)+1,8*(2*traverse+2)+8 };
Physical Volume ( 16 ) = { 8*(2*traverse+2)+2 };
Physical Volume ( 17 ) = { 8*(2*traverse+2)+3 };
Physical Volume ( 18 ) = { 8*(2*traverse+2)+4 };
Physical Volume ( 19 ) = { 8*(2*traverse+2)+5 };
Physical Volume ( 20 ) = { 8*(2*traverse+2)+6 };
Physical Volume ( 21 ) = { 8*(2*traverse+2)+7 };

Coherence;